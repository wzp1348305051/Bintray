// 添加maven插件,用于自动生成pom文件等
apply plugin: 'com.github.dcendents.android-maven'
// 添加bintray插件
apply plugin: 'com.jfrog.bintray'

// 库的包名
group = PROJ_GROUP
// 库的版本
version = PROJ_VERSION

// 定义POM并打包AAR
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // 库的简单描述
                description PROJ_DESCRIPTION

                // 添加自己的描述信息
                name PROJ_NAME
                url PROJ_WEBSITEURL

                // 设置证书信息
                licenses {
                    license {
                        name PROJ_LICENCE_NAME
                        url PROJ_LICENCE_URL
                        distribution PROJ_LICENCE_DESCRIPTION
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection PROJ_VCSURL
                    developerConnection PROJ_VCSURL
                    url PROJ_WEBSITEURL
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs //source指定了源码位置
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

// 这里dependsOn意为仅当javadoc完成后才开始本task
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "android" // 此处对应Bintray上的Repositories的名称
        name = PROJ_NAME
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        licenses = ["Apache-2.0"]
        publish = true
        version {
            desc = PROJ_DESCRIPTION
            gpg {
                sign = true // 是否用GPG签名文件
                passphrase = properties.getProperty("bintray.gpg.password") // GPG密钥口令
            }
        }
    }
}